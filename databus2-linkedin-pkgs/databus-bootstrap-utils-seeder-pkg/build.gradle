apply plugin: 'java'

dependencies {
  compile project(':databus-bootstrap-utils:databus-bootstrap-utils-impl')
  compile project(':databus-client:databus2-client-util')

  compile externalDependency.avro
  compile externalDependency.ojdbc6
}

task myTar(type: Tar) {
  extension = 'tar.gz'
  baseName = project.name
  compression = Compression.GZIP

  into("conf") { from "${project.rootDir}/databus-client/databus2-client-util-pkg/config/" }
  into("conf") { from "config/" }
  
  from("${project.rootDir}/databus-client/databus2-client-util-pkg/script") {
    into ("bin")
    include('**/*.sh')
    fileMode = 0755
  }

  from("script") {
    into ("bin")
    fileMode = 0755
  }
  
  into("bin") { 
    from("${project.rootDir}/database")
    include('**/*')
  }

  into("bin") { 
    from("${project.rootDir}/database")
    include('**/*.sh')
    fileMode = 0755
  }
  
  into("bin") {
    from("${project.rootDir}/script")
    include('setup.inc')
  }

  into("schemas_registry") {
    from("${project.rootDir}/schemas_registry")
    include('**/*')
  }

  into("lib") {
     from configurations.runtime
  }
}

myTar.dependsOn ':databus-bootstrap-utils:databus-bootstrap-utils-impl:assemble'
myTar.dependsOn ':databus-client:databus2-client-util:assemble'
assemble.dependsOn myTar
